# 试做一个计算器


<!-- vscode-markdown-toc -->
* 1. [lex](#lex)
	* 1.1. [.l 文件结构](#l)
	* 1.2. [规则区块中用到的正则](#)
	* 1.3. [lex 的正则匹配规则](#lex-1)
* 2. [yacc](#yacc)
	* 2.1. [yacc 的语法规则](#yacc-1)
	* 2.2. [规约过程](#-1)
		* 2.2.1. [先直观的看一下规约过程](#-1)
		* 2.2.2. [再看一下具体的规约逻辑](#-1)
* 3. [生成执行文件](#-1)
* 4. [冲突](#-1)
* 5. [错误处理](#-1)

<!-- vscode-markdown-toc-config
	numbering=true
	autoSave=true
	/vscode-markdown-toc-config -->
<!-- /vscode-markdown-toc -->


##  1. <a name='lex'></a>lex
1. lex/flex 是自动生成词法分析器的工具，通过输入 `.l` 类型的文件，输出词法分析器的 C 语言代码。
2. 词法分析会将输入的字符串分割为 token，因此需要定义 token 都是哪些类型的字符串。
3. mycalc 用到的 token 字符串包括以下种类
    * 运算符： `+`、`-`、`*`、`/`；
    * 整数；
    * 浮点数；
    * 换行符：一个算式输入后，再输入换行符就会执行计算。
4. lex 中，使用正则表达式定义 token 字符串。为 mycalc 编写的输入文件 `mycalc.l` 代码如下
    ```lex
    %{
    #include <stdio.h>
    #include "y.tab.h"

    int
    yywrap(void)
    {
        return 1;
    }
    %}
    %%
    "+"             return ADD;
    "-"             return SUB;
    "*"             return MUL;
    "/"             return DIV;
    "\n"            return CR;
    ([1-9][0-9]*)|0|([0-9]+\.[0-9]*) {
        double temp;
        sscanf(yytext, "%lf", &temp);
        yylval.double_value = temp;
        return DOUBLE_LITERAL;
    }
    [ \t] ;
    . {
        fprintf(stderr, "lexical error.\n");
        exit(1);
    }
    %%
    ```

###  1.1. <a name='l'></a>.l 文件结构
1. 两个 `%%` 之间的部分是 **规则区块**（rules），规则区块之前的是 **定义区块**（definitions），规则区块之前的是 **用户代码区块**（subroutines）。
2. 在定义区块内，可以定义初始状态或者为正则表达式命名。
3. 定义区块内 `%{` 和 `%}` 包裹的部分，是要让生成的词法分析器将此部分代码原样输出。后续程序所需的头文件 `#include` 等都包含在这里，比如 y.tab.h 就是之后 yacc 自动生成的头文件，规则区块中用到的 `ADD` 等宏就是在 y.tab.h 中定义的。y.tab.h 中实际的定义是写在 `mycalc.y` 中。
4. 定义区块里还定义了一个 `yywrap` 函数。如果没有这个函数，就必须手动链接 lex 的库文件。不懂。
5. 之后是规则区块，里面的每一个规则，都是一个正则表达式跟着若干个空格然后再加一段 C 代码。如果输入的字符串匹配到了这条规则的正则表达式，则该条规则后面的 C 代码就会执行。这段 C 代码称为这条规则的 **动作**（action）。
6. 最后是用户代码区块，其中可以编写任意的 C 代码。

###  1.2. <a name=''></a>规则区块中用到的正则
1. lex 的正则可以使用 `""` 转义，上面对加减乘除和换行都使用了这样的转义。如果匹配到了这里的运算符或者换行符，则执行后面的动作，返回对应的特征符。所谓特征符，就是定义在 y.tab.h 中的用来识别某个 token 符号的代号。
2. `([1-9][0-9]*)|0|([0-9]+\.[0-9]*)` 匹配实数。但实际上还能匹配到 `000`、`001`、`00.` 和 `00.1` 这样的写法。如果匹配到了，匹配到的字符串会保存在全局变量 `yytext` 中，并在 action 中被 `sscanf` 解析，解析后的值保存在全局的联合体 `yylval` 的 `double_value` 字段中。联合体 `yylval` 也是定义在 `mycalc.y` 中。
3. `[ \t]` 匹配空格或制表符。动作对应为空，因此可以忽略每一行的空白字符。
4. 最后的 `.` 匹配任意一个字符。用于检测是否输入了程序不允许的字符。相当于 `switch...case` 的 `default` 分支。

###  1.3. <a name='lex-1'></a>lex 的正则匹配规则
1. 正则会尽可能的选择较长的字符串进行匹配。例如 C 语言中有 `+` 和 `++` 运算符。那么当输入是 `++` 时，lex 会优先匹配为 `++` 而不是 `+`。
2. 如果两个规则都匹配到同样的长度的字符串时，则会优先使用前面那个规则进行匹配。上面 lex 的最后一条正则 `.` 实际上可以匹配到每一个字符，但因为它是最后一条规则，所以只有在前面的规则都匹配不了时，它才会进行匹配。


##  2. <a name='yacc'></a>yacc
1. yacc 是自动生成语法分析器的工具，输入 `.y` 类型的文件，就会输出语法分析器的 C 语言代码。
2. mycalc 中的 yacc 输入文件 `mycalc.y` 代码如下
    ```yacc
    %{
    #include <stdio.h>
    #include <stdlib.h>
    #define YYDEBUG 1
    %}
    %union {
        int          int_value;
        double       double_value;
    }
    %token <double_value>      DOUBLE_LITERAL
    %token ADD SUB MUL DIV CR
    %type <double_value> expression term primary_expression
    %%
    line_list
        : line
        | line_list line
        ;
    line
        : expression CR
        {
            printf(">>%lf\n", $1);
        }
    expression
        : term
        | expression ADD term
        {
            $$ = $1 + $3;
        }
        | expression SUB term
        {
            $$ = $1 - $3;
        }
        ;
    term
        : primary_expression
        | term MUL primary_expression 
        {
            $$ = $1 * $3;
        }
        | term DIV primary_expression
        {
            $$ = $1 / $3;
        }
        ;
    primary_expression
        : DOUBLE_LITERAL
        ;                 
    %%
    int
    yyerror(char const *str)
    {
        extern char *yytext;
        fprintf(stderr, "parser error near %s\n", yytext);
        return 0;
    }

    int main(void)
    {
        extern int yyparse(void);
        extern FILE *yyin;

        yyin = stdin;
        if (yyparse()) {
            fprintf(stderr, "Error ! Error ! Error !\n");
            exit(1);
        }
    }
    ```
3. 最前面 `%{` 和 `%}` 包裹的部分仍然是要原样输出的 C 代码。其中 `#define YYDEBUG 1` 是开启 debug 模式，可以看到程序运行中语法分析的状态。
4. 紧接着的那个 union 定义了 token 和非终端符的类型。非终端由多个 token 共同组成，代码中下面的 `line_list`、`line`、`expression` 和 `term` 都是非终端。虽然书上没说，但是从 yacc 生成的 y.output 中来看，`primary_expression` 也是非终端。非终端最后都会以一个终端符结尾。蛋仔 mycalc 的例子里实际上没有 `int_value` 类型的值，整数也是用浮点数表示的。
5. 之后的 10~11 行是 token 的声明。mycalc 所用到的 token 种类都在这里定义。DOUBLE_LITERAL 这个 token 被声明为 `double_value` 类型，该类型就是上面在 union 中定义的。`ADD`、`SUB`、 `MUL`、`DIV` 和 `CR` 只是操作符，不涉及数据类型。
6. 再下面是和 lex 一样的规则区块。yacc 的规则区块，由语法规则以及 C 语言编写的相应动作组成。
7. 最后是用户代码区块，里面有 C 必需的 `main` 函数。

###  2.1. <a name='yacc-1'></a>yacc 的语法规则
1. 再 yacc 中，会使用类似 BNF（Backus–Naur form，Backus Normal form，巴科斯范式）。
2. 再上例中，第一个规则是 line_list，关于多行代码的。


###  2.2. <a name='-1'></a>规约过程
根据上面的语法规则，以输入为 `1 + 2 * 3`，看看对其的规约过程。

####  2.2.1. <a name='-1'></a>先直观的看一下规约过程
1. 根据设定的语法规则，`1 + 2 * 3` 被规约为 `primary_expression + primary_expression * primary_expression`。
2. 由因为 `term` 可以是 `primary_expression`，所以进一步规约为 `term + term * primary_expression`。
3. 最后一个 `primary_expression` 没有规约为 `term`，因为根据 `term` 的规则 `term MUL primary_expression`， `term * primary_expression` 可以规约为 `term`，并且同时会执行乘法操作 `$$ = $1 * $3;`。
4. 因此整体规约为 `term + term`。又因为 `expression` 可以是 `term`，那么 `term + term` 就可以规约为  `expression + term`。
5. 此时，根据 `expression` 的语法规则，`expression + term` 就可以进一步规约为 `expression`，同时执行加法操作 `$$ = $1 + $3;`。
6. 然后用户最后输入的的是换行符，那现在整体的输入就是 `expression CR`，规约为 `line`，执行的动作是 `printf` 打印计算结果。

####  2.2.2. <a name='-1'></a>再看一下具体的规约逻辑
1. yacc 生成的解析器会保存在程序内部栈结构中。token 入栈的过程称为 **移进**（shift）。
2. `1` 首先入栈。`1` 是 `DOUBLE_LITERAL` 类型，符合 `primary_expression` 语法规则，所以 `1` 被规约为 `primary_expression`。又因为 `primary_expression` 符合 `term` 的语法规则，所以进一步规约为 `term`；同理，`term` 又进一步规约为 `expression`。因此栈底现在是 `expression`。
3. 接下来是 `+` 入栈，不符合任何语法规则，所以不做规约。不过它对应的 token 记号是 `ADD`。
4. 然后是 `2` 入栈。首先规约为 `primary_expression`，现在栈里是 `expression ADD primary_expression`，不符合任何语法规则；`primary_expression` 可以再规约为 `term`，所以栈里现在是 `expression ADD term`，此时符合了 `expression` 的语法规则；
5. 但是 `expression ADD term` 并没有规约为 `expression`。而是先执行了移进，让之后的 `*` 和 `3` 入栈，栈里变成 `expression ADD term MUL primary_expression`，并让后面的 `term MUL primary_expression` 优先规约为 `term`。不懂，为什么要让后面的优先规约，那如果输入时 `3-2-1`，那不就会规约成 `3-1` 了吗？或者说，为什么不先让前面的规约，而是先让后的移进，并且让后面的优先规约？
6. 现在栈里是 `expression ADD term`，规约 `expression`。


##  3. <a name='-1'></a>生成执行文件
1. mycalc.y 输入给 yacc 生成 y.tab.c 和 y.tab.h，mycalc.l 输入给 lex 生成 lex.yy.c。
2. 为了将 myucalc.y 中定义的 token 及 union 传递给 lex.yy.c，所以 yacc 会生成 y.tab.h。


##  4. <a name='-1'></a>冲突
TODO


##  5. <a name='-1'></a>错误处理
1. 最省事的解决方法之一就是，一旦出错，立即使用 `exit()` 退出。
2. 但是对于计算器来说，是需要与用户互动，如果输错了一点程序就强制退出的话，对用户也太不友好了。因此我们可以利用 yacc 的功能实现一个简单的错误恢复机制。
3. 首先在 `mycalc.y` 的非终结符 `line` 的语法规则中，追加下面的部分
    ```yacc
    line
        : expression CR
        {
            printf(">>%lf\n", $1);
        }
        | error CR
        {
            yyclearin;
            yyerrok;
        }
        ;
    ```
4.  `error` 记号是匹配错误的特殊记号。 `error` 可以后接 `CR`（换行符），这样书写可以匹配包含了错误的所有记号以及行尾。
5. 动作中的 `yyclearin` 会丢弃预读的记号，而 `yyerrok` 则会通知 yacc 程序已经从错误状态恢复了。
